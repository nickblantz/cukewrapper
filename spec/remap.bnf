Remap   : JSONText 
        | Merge
        | Eval
        ;

Merge   : MergePrefix, [ MergeRoot ];

MergePrefix : '~';

MergeRoot   : Eval
            | JSONObject
            | JSONArray
            ;
                    
Eval    : EvalPrefix, [ VALID_RUBY_CODE ];

EvalPrefix  : '#';

JSONText    : JSONValue;

JSONValue   : JSONNullLiteral
            | JSONBooleanLiteral
            | JSONString
            | JSONNumber
            | JSONObject
            | JSONArray
            ;

JSONString  : STRING;

JSONNullLiteral : NULL;

JSONNumber  : NUMBER;

JSONBooleanLiteral  : TRUE
                    | FALSE
                    ;

JSONObject  : '{' '}'
            | '{' JSONMemberList '}'
            ;

JSONMember  : JSONString ':' JSONValue;

JSONMemberList  : JSONMember
                | JSONMemberList ',' JSONMember
                ;

JSONArray   : '[' ']'
            | '[' JSONElementList ']'
            ;

JSONElementList : JSONValue
                | JSONElementList ',' JSONValue
                ;